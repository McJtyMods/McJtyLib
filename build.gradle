buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish'

repositories {
    maven { url 'https://www.dogforce-games.com/maven/' }
    maven { // JEI
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven { // Patchouli
        url "https://maven.blamejared.com"
    }
    maven { // TOP
        url "https://maven.k-4u.nl"
    }
}

group= "com.github.mcjty" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "mcjtylib"

if (System.getenv('BUILD_NUMBER') != null) {
    version += ".${System.getenv('BUILD_NUMBER')}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
//    mappings channel: 'parchment', version: "2022.06.05-1.18.2"
    mappings channel: 'official', version: "1.19.3"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                mcjtylib {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            mods {
                mcjtylib {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'mcjtylib', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                mcjtylib {
                    source sourceSets.main
                }
            }
        }
    }
}

jar {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    if (findProject(':TheOneProbe') != null) {
        implementation project(':TheOneProbe')
    } else {
        implementation fg.deobf(project.dependencies.create("mcjty.theoneprobe:theoneprobe:${top_version}") {
            transitive = false
        })
    }

    // compile against the JEI API but do not include it at runtime
    compileOnly fg.deobf("mezz.jei:jei-1.19-common-api:${jei_version}")
    compileOnly fg.deobf("mezz.jei:jei-1.19-forge-api:${jei_version}")
    // at runtime, use the full JEI jar for Forge
    // runtimeOnly fg.deobf("mezz.jei:jei-1.19-forge:${jei_version}")
    //implementation fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}") {
//        transitive = false
//    }
    //deobfCompile "mcp.mobius.waila:Hwyla:${waila_version}"
    //deobfCompile "cofh:RedstoneFlux:${redstoneflux_version}"
    //deobfCompile "net.darkhax.tesla:Tesla-1.12.2:1.0.63"

    //implementation "li.cil.oc:OpenComputers:${oc_version}"
}

jar {
    manifest {
        attributes 'FMLAT': 'accesstransformer.cfg',
        "Specification-Title": "mcjtylib",
        "Specification-Vendor": "McJty",
        "Specification-Version": "1",
        "Implementation-Title": project.name,
        "Implementation-Version": "${version}",
        "Implementation-Vendor" :"McJty",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
}

//processResources
//{
//    // this will ensure that this task is redone when the versions change.
//    inputs.property "version", project.version
//    inputs.property "mcversion", project.minecraft.version
//
//    // replace stuff in mcmod.info, nothing else
//    from(sourceSets.main.resources.srcDirs) {
//        include 'mcmod.info'
//                
//        // replace version and mcversion
//        expand 'version':project.version, 'mcversion':project.minecraft.version
//    }
//        
//    // copy everything else, thats not the mcmod.info
//    from(sourceSets.main.resources.srcDirs) {
//        exclude 'mcmod.info'
//    }
//}

task apiJar(type: Jar) {
    classifier = "api"
    from(sourceSets.main.output) {
        include "mcjty/mcjtylib/api/**"
    }
}

artifacts {
    archives apiJar
}

publishing {
    publications.create('mavenJava', MavenPublication) {
        from components.java
        artifact apiJar

        artifactId = project.archivesBaseName
        pom {
            name = 'McJtyLib'
            description = 'Base mod for all McJty tech mods'
            scm {
                url = 'https://github.com/McJtyMods/McJtyLib'
                connection = 'scm:git:git@github.com:McJtyMods/McJtyLib.git'
                developerConnection = 'scm:git:git@github.com:McJtyMods/McJtyLib.git'
            }
            issueManagement {
                system = 'github'
                url = 'https://github.com/McJtyMods/McJtyLib/issues'
            }
            licenses {
                license {
                    name = 'MIT'
                    distribution = 'repo'
                }
            }
            developers {
                developer {
                    id = 'McJty'
                    name = 'McJty'
                    roles = ['developer']
                }
            }
        }
    }
    repositories {
        if (System.getenv('local_maven') != null) {
            maven {
                name = 'local'
                url = 'file://' + System.getenv('local_maven')
            }
        }
    }
}


//task("uploadJars", dependsOn: "reobf") {
//    description = "uploads JARs"
//    if (System.getenv("local_maven") != null) {
//        apply plugin: 'maven'
//        uploadArchives {
//            repositories {
//                mavenDeployer {
//                    repository(url: "file://" + System.getenv("local_maven"))
//                    pom {
//                        groupId = project.group
//                        version = project.version
//                        artifactId = project.archivesBaseName
//                        project {
//                            name project.archivesBaseName
//                            packaging 'jar'
//                            description 'McJtyLib'
//                            scm {
//                                url 'https://github.com/McJtyMods/McJtyLib'
//                                connection 'scm:git:git@github.com:McJtyMods/McJtyLib.git'
//                                developerConnection 'scm:git:git@github.com:McJtyMods/McJtyLib.git'
//                            }
//                            issueManagement {
//                                system 'github'
//                                url 'https://github.com/McJtyMods/McJtyLib/issues'
//                            }
//                            licenses {
//                                license {
//                                    name 'MIT'
//                                    distribution 'repo'
//                                }
//                            }
//                            developers {
//                                developer {
//                                    id 'McJty'
//                                    name 'McJty'
//                                    roles { role 'developer' }
//                                }
//                            }
//                        }
//                    }
//                }
//            }
//        }
//    }
//}

String getChangelogText() {
    def changelogFile = file('changelog.txt')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            def temp = it
            if (lineCount == 0) {
                temp = "${modname} ${version}"
                temp = "<h2>$temp</h2>"
            } else if (it.startsWith('-')) {
                temp = "&nbsp;&nbsp;&nbsp;$temp"
                temp = temp.replaceAll("(\\S+\\/\\S+)#([0-9]+)\\b", "<a href=\"https://github.com/\$1/issues/\$2\">\$0</a>");
                temp = temp.replaceAll("#([0-9]+)\\b(?!<\\/a>)", "<a href=\"https://github.com/$github_project/issues/\$1\">\$0</a>");
            } else {
                temp = "<h4>$temp</h4>"
            }
            str += "$temp<br/>"
            lineCount++
            return
        } else {
            done = true
        }
    }
    return str
}

tasks.curseforge.enabled = project.hasProperty('curseforge_key')

curseforge {
    if (project.hasProperty('curseforge_key')) {
        apiKey = project.curseforge_key
    }

    project {
        id = project.projectId
        changelog = System.getenv('CHANGELOG') == null || System.getenv('CHANGELOG').equals('none') ? getChangelogText() : System.getenv('CHANGELOG')
        changelogType = 'html'
        releaseType = project.curse_type
        addGameVersion project.minecraft_version
        mainArtifact(jar) {
            displayName = "${modname} - ${version}"
        }
        relations {
            optionalLibrary 'redstone-flux'
            optionalLibrary 'the-one-probe'
        }
    }
}

